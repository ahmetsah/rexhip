FUNCTION_BLOCK "Vacon_OPTC2_C8"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      mb_addr : UInt;
      ref : Real;
   END_VAR

   VAR_OUTPUT 
      actual_speed : Real;
      Output_Frequency : Real;   // Hz
      Motor_Speed : Real;   // rpm
      Motor_Current : Real;   // A
      Motor_Torque : Real;   // %
      Motor_Power : Real;   // %
      Motor_Voltage : Real;   // V
      DC_link_voltage : Real;   // V
   END_VAR

   VAR_IN_OUT 
      mb_query : "mb_query";
   END_VAR

   VAR 
      sb : "mb_station_block_udt";
      write : Struct
         ctw : Struct
            byte0 : Bool;
            x00_run : Bool;
            x01_reverse : Bool;
            x02_reset : Bool;
            x03 : Bool;
            x04 : Bool;
            x05 : Bool;
            x06 : Bool;
            x07 : Bool;
         END_STRUCT;
         "General Control Word" : Word;
         speed_reference : Int;
      END_STRUCT;
      read : Struct
         status : Struct
            x08_TC_Speed_Limit_Not_Active : Bool;
            x09_Detected_Encoder_Direction_Counterclockwise : Bool;
            x10_UV_Fast_Stop_Not_Active : Bool;
            x11 : Bool;
            x12 : Bool;
            x13 : Bool;
            x14 : Bool;
            x15 : Bool;
            x00_ready : Bool;
            x01_RUN : Bool;
            x02_reverse : Bool;
            x03_Faulted : Bool;
            x04_Warning : Bool;
            x05_Ref_Frequency_reached : Bool;
            x06_Motor_is_running_at_zero_speed : Bool;
            x07_Flux_Ready : Bool;
         END_STRUCT;
         general_status : Struct
            x08 : Bool;
            x09 : Bool;
            x10 : Bool;
            x11 : Bool;
            x12 : Bool;
            x13_Fieldbus_control : Bool;
            "x14_Panel control" : Bool;
            x15_IO_control : Bool;
            x00 : Bool;
            x01 : Bool;
            x02 : Bool;
            x03 : Bool;
            x04 : Bool;
            x05 : Bool;
            x06 : Bool;
            x07 : Bool;
         END_STRUCT;
         actual_speed : Int;
         Output_Frequency : Int;   // 0.01 Hz
         Motor_Speed : Int;   // 1 rpm
         Motor_Current : Int;   // 0.1 A
         Motor_Torque : Int;   // 0.1 %
         Motor_Power : Int;   // 0.1 %
         Motor_Voltage : Int;   // 0.1 V
         DC_link_voltage : Int;   // 1 V
         Active_Fault_Code : Int;
      END_STRUCT;
   END_VAR


BEGIN
	// Vacon NX - OPTC2/C8 - AC drives
	// Modbus/N2 option board
	// Document code: DPD00899A , Date 19.01.2012
	// Web: http://www.vacon.com/ImageVaultFiles/id_3118/cf_2/
	//      Vacon-NX-OPTC2-C8-Modbus-N2-Board-User-Manual-DPD0.
	//      PDF?635223804391930000
	
	"mb_station_block_header"(sb := #sb, mb_query := #mb_query);
	#mb_query.mb_addr := #mb_addr;
	
	#mb_query.mode := #mb_query.c.read.holding_reg;
	
	#mb_query(data_addr := 42101, data_ptr := #read);
	#actual_speed := INT_TO_REAL(#read.actual_speed);
	#Output_Frequency := INT_TO_REAL(#read.Output_Frequency) * 0.01;
	#Motor_Speed := INT_TO_REAL(#read.Motor_Speed);
	#Motor_Current := INT_TO_REAL(#read.Motor_Current) * 0.1;
	#Motor_Torque := INT_TO_REAL(#read.Motor_Torque) * 0.1;
	#Motor_Power := INT_TO_REAL(#read.Motor_Power) * 0.1;
	#Motor_Voltage := INT_TO_REAL(#read.Motor_Voltage) * 0.1;
	#DC_link_voltage := INT_TO_REAL(#read.DC_link_voltage);
	
	// -------------------------------------------------------
	#mb_query.mode := #mb_query.c.write.holding_reg;
	
	#write.ctw.x00_run := #ref <> 0;
	#mb_query(data_addr := 42001, data_ptr := #write.ctw);
	#write.speed_reference := REAL_TO_INT(#ref * 100);
	#mb_query(data_addr := 42003, data_ptr := #write.speed_reference);
	
	"mb_station_block_footer"(sb := #sb, mb_query := #mb_query);
	
END_FUNCTION_BLOCK

