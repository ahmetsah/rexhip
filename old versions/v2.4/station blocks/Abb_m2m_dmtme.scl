FUNCTION_BLOCK "Abb_M2M_DMTME"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT DB_SPECIFIC
      mb_addr : UInt;
   END_VAR

   VAR_IN_OUT 
      mb_query : "mb_query";
   END_VAR

   VAR DB_SPECIFIC
      sb : "mb_station_block_udt";
   END_VAR
   VAR 
      read1 { ExternalWritable := 'False'} : Struct
         voltage_3phase { ExternalWritable := 'False'} : UDInt;   // volt
         voltage_Lx_N { ExternalWritable := 'False'} : Array[1..3] of UDInt;   // volt
         voltage_L_L { ExternalWritable := 'False'} : Array[1..3] of UDInt;   // volt 1:L1-L2, 2:L2-L3, 3:L3-L1
         current_3phase { ExternalWritable := 'False'} : UDInt;
         current { ExternalWritable := 'False'} : Array[1..3] of UDInt;   // mA
         power_factor_3phase { ExternalWritable := 'False'} : DInt;   // x1000
         power_factor { ExternalWritable := 'False'} : Array[1..3] of DInt;   // x1000
         phase_cos_3phase { ExternalWritable := 'False'} : DInt;   // x1000
         phase_cos { ExternalWritable := 'False'} : Array[1..3] of DInt;   // x1000
      END_STRUCT;
      read2 { ExternalWritable := 'False'} : Struct
         apparent_power_3phase { ExternalWritable := 'False'} : UDInt;   // VA
         apparent_power { ExternalWritable := 'False'} : Array[1..3] of UDInt;   // VA
         active_power_3phase { ExternalWritable := 'False'} : DInt;   // Watt
         active_power { ExternalWritable := 'False'} : Array[1..3] of DInt;   // Watt
         reactive_power_3phase { ExternalWritable := 'False'} : UDInt;
         reactive_power { ExternalWritable := 'False'} : Array[1..3] of UDInt;
         active_energy_3phase { ExternalWritable := 'False'} : UDInt;   // Wh * 100
         reactive_energy_3phase { ExternalWritable := 'False'} : UDInt;   // VArh * 100
         frequency { ExternalWritable := 'False'} : UDInt;   // mHz
         max_line_current { ExternalWritable := 'False'} : Array[1..3] of UDInt;
      END_STRUCT;
   END_VAR


BEGIN
	// ABB - M2M/DMTME - Network analyser
	// Product ID: 2CSG445011D0201, EAN: 8012542998938
	// 
	// https://library.e.abb.com/public/3921770555944801ad6d9e002d23ca23/2CSG445011D0201.pdf
	
	"mb_station_block_header"(sb := #sb, mb_query := #mb_query);
	#mb_query.mb_addr := #mb_addr;
	
	#mb_query.mode := #mb_query.c.read.holding_reg;
	#mb_query(data_addr := 16#1000, data_ptr := #read1);
	#mb_query(data_addr := 16#1026, data_ptr := #read2);
	
	"mb_station_block_footer"(sb := #sb, mb_query := #mb_query);
	
END_FUNCTION_BLOCK
