FUNCTION_BLOCK "Rotork_Mk_3_option_card"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      mb_addr : UInt;
   END_VAR

   VAR_IN_OUT 
      mb_query : "mb_query";
   END_VAR

   VAR 
      sb : "mb_station_block_udt";
      read1 : Struct
         x00_actuator_moving : Bool;
         "x01_Close position limit" : Bool;
         "x02_Open position limit" : Bool;
         "x03_Valve running close" : Bool;
         "x04_Valve running open" : Bool;
         "x05_Selector in remote" : Bool;
         "x06_Selector in local stop" : Bool;
         "x07_Selector in local" : Bool;
         "x08_Thermostat tripped" : Bool;
         "x09_Monitor relay" : Bool;
         "x10_Valve obstructed" : Bool;
         "x11_Valve jammed" : Bool;
         "x12_Manual movement" : Bool;
         "x13_MIT_Interrupter timer" : Bool;
         "x14_Position control enabled" : Bool;
         "x15_Watchdog Recovery" : Bool;
         "x16_Battery low" : Bool;
         "x17_Open Interlock input active" : Bool;
         "x18_Close Interlock input active" : Bool;
         "x19_Hard wired input DI-1 (open)" : Bool;
         "x20_Hard wired input DI-2 (close)" : Bool;
         "x21_Hard wired input DI-3 (stop)" : Bool;
         "x22_Hard wired input DI-4 (ESD)" : Bool;
         x23 : Bool;
         x24 : Bool;
         x25 : Bool;
         "x26_Control contention" : Bool;
         "x27_Partial stroke test in progress" : Bool;
         "x28_Partial stroke test error" : Bool;
         "x29_General alarm" : Bool;
         x30 : Bool;
         x31 : Bool;
      END_STRUCT;
      read2 : Struct
         instantaneous_torque : Int;
         valve_position : Int;   // x10 %
         analogue_input : Int;
      END_STRUCT;
      write1 : Struct
         stop : Bool;
         close : Bool;
         open : Bool;
         emergency_shut_down : Bool;
         output_DO_1 : Bool;
         output_DO_2 : Bool;
         output_DO_3 : Bool;
         output_DO_4 : Bool;
      END_STRUCT;
      write2 : Struct
         actuator_digital_control : Bool;
         position_demand_output : Bool;
      END_STRUCT;
      actuator_tag_name : String[12];
      action_on_loss_of_comms : Int;
   END_VAR


BEGIN
	// Rotork  - Mk 3 Option Card
	
	// http://www.rotork.com/en/product/index/modbusliterature
	// Publication PUB091-004-00_0715
	
	"mb_station_block_header"(sb := #sb, mb_query := #mb_query);
	#mb_query.mb_addr := #mb_addr;
	
	#mb_query.mode := #mb_query.c.read.discrete_input;
	#mb_query(data_addr := 0, data_ptr := #read1);
	
	#mb_query.mode := #mb_query.c.read.holding_reg;
	#mb_query(data_addr := 2, data_ptr := #read2);
	#mb_query(data_addr := 26, data_ptr := #actuator_tag_name);
	
	#mb_query.mode := #mb_query.c.write.discrete_output;
	#mb_query(data_addr := 0, data_ptr := #write1);
	
	#mb_query.mode := #mb_query.c.write.holding_reg;
	// 0=No-Action, 1=Open, 3=Close, 5=Stop, 7=Position
	#mb_query(data_addr := 7, data_ptr := #action_on_loss_of_comms);
	#mb_query(data_addr := 5, data_ptr := #write2);
	
	"mb_station_block_footer"(sb := #sb, mb_query := #mb_query);
	
END_FUNCTION_BLOCK

