FUNCTION_BLOCK "Siemens_Sinamics_G120"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      mb_addr : UInt;
      stop : Bool;
      ref : Real;
   END_VAR

   VAR_IN_OUT 
      mb_query : "mb_query";
   END_VAR

   VAR 
      sb : "mb_station_block_udt";
      read : Struct
         stw : Struct
            x08_Speed_setpoint : Bool;   // Actual value deviation within tolerance t_off
            x09_Control_requested : Bool;
            x10_f_or_n_comparison_value_reached_exceeded : Bool;
            x11_I_M_or_P_limit_not_reached : Bool;
            x12 : Bool;
            x13_No_motor_overtemperature_alarm : Bool;
            x14_Motor_rotates_forwards : Bool;   // 0 = Motor rotates backwards (n_act < 0)
            x15_No_alarm_thermal_overload_power_unit : Bool;
            x00_Ready_to_power_up : Bool;
            x01_Ready_to_operate : Bool;   // (DC link loaded, pulses blocked)
            x02_Operation_enabled : Bool;   // (drive follows n_set)
            x03_Fault_present : Bool;
            x04_No_coast_down_active : Bool;   // (OFF2 inactive)
            x05_No_fast_stop_active : Bool;   // (OFF3 inactive)
            x06_power_on_inhibit_active : Bool;
            x07_Alarm_present : Bool;
         END_STRUCT;
         actual : Int;
      END_STRUCT;
      write : Struct
         ctw : Struct
            x08 : Bool;
            x09 : Bool;
            x10_control_via_PLC : Bool := TRUE;
            x11_reversal : Bool;
            x12 : Bool;
            x13_Motorized_potentiometer_setpoint_raise : Bool;
            x14_Motorized_potentiometer_setpoint_lower : Bool;
            x15 : Bool;
            x00_R_TRIG_ON : Bool;   // 0 = OFF1 (braking with ramp-function generator, then pulse cancllation &ready-to-power-up)
            x01_No_OFF2 : Bool := TRUE;   // enable is possible. 0 = OFF2 (immediate pulse cancellation and power-on inhibit)
            x02_No_OFF3 : Bool := TRUE;   // enable is possible. 0 = OFF3 braking with the OFF3 ramp p1135, then pulse cancellation andpower-on inhibit)
            x03_Enable_operation : Bool := TRUE;   // (pulses can be enabled) 0 = Inhibit operation (cancel pulses)
            x04_Operating_condition : Bool;   // (the ramp-function generator can be enabled)0 = Inhibit ramp-function generator (set the ramp-function generator output tozero)
            x05_Enable_the_ramp_function_generator : Bool;   // 0 = Stop the ramp-function generator (freeze the ramp-function generatoroutput)
            x06_enable_setpoint : Bool := TRUE;   // 0 = Inhibit setpoint (set the ramp-function generator input to zero)
            x07_R_TRIG_acknowledge_faults : Bool;
         END_STRUCT;
         setpoint : Int;
      END_STRUCT;
   END_VAR


BEGIN
	// Siemens - SINAMICS G120
	// 
	// Web: https://cache.industry.siemens.com/dl/files/944/
	// 35928944/att_7966/v1/application_cu230_modbus_v1_2_en.pdf
	
	"mb_station_block_header"(sb := #sb, mb_query := #mb_query);
	#mb_query.mb_addr := #mb_addr;
	
	#mb_query.mode := #mb_query.c.read.holding_reg;
	#mb_query(data_addr := 40110, data_ptr := #read);
	
	// -- WRITE --
	#mb_query.mode := #mb_query.c.write.holding_reg;
	
	#write.ctw.x00_R_TRIG_ON := NOT #stop AND NOT #read.stw.x07_Alarm_present
	AND #read.stw.x01_Ready_to_operate AND #read.stw.x00_Ready_to_power_up;
	#write.setpoint := REAL_TO_INT(#ref);
	#mb_query(data_addr := 40100, data_ptr := #write);
	
	"mb_station_block_footer"(sb := #sb, mb_query := #mb_query);
END_FUNCTION_BLOCK

